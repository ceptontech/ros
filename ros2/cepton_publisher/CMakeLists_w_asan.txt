cmake_minimum_required(VERSION 3.5)
project(cepton_publisher)

# Default to C99
if(NOT CMAKE_C_STANDARD)
  set(CMAKE_C_STANDARD 99)
endif()

 
# Default to C++17
if(NOT CMAKE_CXX_STANDARD)
  set(CMAKE_CXX_STANDARD 17)
endif()

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  add_compile_options(-Wall -Wextra -Wpedantic -fPIC -fsanitize=address) # Added ASan flag here
  add_compile_options(-Wno-cast-function-type) # suppress function pointer cast stderr messages for CI
endif()

# find the non-sdk dependencies
find_package(ament_cmake REQUIRED)
find_package(rclcpp REQUIRED)
find_package(std_msgs REQUIRED)
find_package(sensor_msgs REQUIRED)
find_package(tf2 REQUIRED)
find_package(geometry_msgs REQUIRED)
find_package(rclcpp_components REQUIRED)

###################################################################
# Link the SDK, first get the binaries for the correct architecture
if (CMAKE_SYSTEM_PROCESSOR MATCHES "amd64.*|x86_64.*|AMD64.*")
  set(SDK_SOURCE ${PROJECT_SOURCE_DIR}/binaries/linux-x64)
elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^(aarch64.*|AARCH64.*|arm64.*|ARM64.*)")
  set(SDK_SOURCE ${PROJECT_SOURCE_DIR}/binaries/linux-arm64)
else()
message("NO LIBRARY FOUND MATCHING PROCESSOR")
endif()
# Then glob the shared objects
file(GLOB SDK_BINARIES ${SDK_SOURCE}/*)

set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_RPATH}:\$ORIGIN/..")
set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_RPATH}:\$ORIGIN/../lib")

# -------------------------------------------------------------------------------
# Compile CeptonPublisher
# -------------------------------------------------------------------------------

find_package(cepton_messages REQUIRED)
add_library(${PROJECT_NAME} SHARED src/cepton_publisher.cpp src/sdk_proxy.h src/panic.h)
target_include_directories(
  ${PROJECT_NAME} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/include>
                          $<INSTALL_INTERFACE:${PROJECT_NAME}/include>)

include_directories(${CeptonSDK2_INCLUDE_DIR})

# Link ros dependencies
ament_target_dependencies(${PROJECT_NAME} PUBLIC rclcpp_components rclcpp
                          std_msgs sensor_msgs cepton_messages)

# Install
install(
  TARGETS ${PROJECT_NAME}
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin)

# -------------------------------------------------------------------------------
# Other setup - Install launch file
# -------------------------------------------------------------------------------

install(DIRECTORY launch DESTINATION share/${PROJECT_NAME}/)

# -------------------------------------------------------------------
# Publisher standalone publisher node
# -------------------------------------------------------------------

add_executable(cepton_publisher_node src/main.cpp)
target_link_libraries(cepton_publisher_node cepton_publisher -fsanitize=address) # added -fsanitize=address
install(TARGETS cepton_publisher_node DESTINATION lib/${PROJECT_NAME})

# Install SDK dependencies to the node location
install(FILES ${SDK_BINARIES} DESTINATION lib)

ament_package()